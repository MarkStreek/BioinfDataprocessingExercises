"""
The main file of this project. All the rules will be imported from seperate files.
Goal: 
    Create a de novo assembly from paired end sequences with quality assesments
Author: 
    Mark van de Streek
Date: 
    2024-03-16
Copyright: 
    BFV3 2023-2024

Run pipeline:
snakemake -c 1 --use-conda --conda-frontend conda
"""

configfile: "config/config.yaml",
workdir: config['workdir'],

rule all:
    input:
        # The FASTQC Tool output isn't used in any rule,
        # therefore, we must specify this in the rule all.
        expand("results/fastqc/{sample}_1_fastqc.html", sample=config['samples']),
        expand("results/fastqc/{sample}_2_fastqc.html", sample=config['samples']),
        expand("results/fastx/filtered_{sample}_1.fastq", sample=config['samples']),
        expand("results/fastx/filtered_{sample}_2.fastq", sample=config['samples']),

        fas=expand("results/trinity/{sample}_trinity.fasta", sample=config['samples']),
        map=expand("results/trinity/{sample}_trinity.fasta.gene_trans_map", sample=config['samples']),

# rule download_Samples:
#     output:
#         config["samples_directory"] + "{sample}_1.fastq",
#         config["samples_directory"] + "{sample}_2.fastq"
#     log:
#         "logs/download/{sample}.log"
#     shell:
#         "fasterq-dump {wildcards.sample} -O {config['samples_directory']}"

rule get_fastq_pe:
    output:
        config["samples_directory"] + "{sample}_1.fastq",
        config["samples_directory"] + "{sample}_2.fastq",
    log:
        "logs/download/{sample}.log"
    params:
        extra="--skip-technical"
    threads:
        config['threads']
    wrapper:
        "v3.5.3/bio/sra-tools/fasterq-dump"

rule fastQC:
    input:
        config["samples_directory"] + "{sample}_1.fastq",
        config["samples_directory"] + "{sample}_2.fastq"
    output:
        "results/fastqc/{sample}_1_fastqc.html",
        "results/fastqc/{sample}_1_fastqc.zip",
        "results/fastqc/{sample}_2_fastqc.html",
        "results/fastqc/{sample}_2_fastqc.zip",
    message:
        "Running FastQC on {input}"
    conda:
        "../envs/FASTQC.yaml"
    params:
        threads=config['threads']
    shell:
        "fastqc -t {params.threads} {input} -o results/fastqc/"

rule fastx_toolkit:
    """
    This tool has verbose specified, but runs first all operations, 
    and then prints the output.
    """
    input:
        s1=config["samples_directory"] + "{sample}_1.fastq",
        s2=config["samples_directory"] + "{sample}_2.fastq"
    output:
        s1="results/fastx/filtered_{sample}_1.fastq",
        s2="results/fastx/filtered_{sample}_2.fastq"
    message:
        "Running fastx_toolkit on {input}"
    conda:
        "../envs/FASTX_TOOLKIT.yaml"
    shell:
        """
        fastq_quality_filter -v -q 20 -p 75 -i {input.s1} -o {output.s1}
        fastq_quality_filter -v -q 20 -p 75 -i {input.s2} -o {output.s2}
        """

rule trimmomatic_pe:
    input:
        r1="results/fastx/filtered_{sample}_1.fastq",
        r2="results/fastx/filtered_{sample}_2.fastq"
    output:
        r1="results/trimmomatic/{sample}_1_paired_trimmed.fastq",
        r2="results/trimmomatic/{sample}_2_paired_trimmed.fastq",
        r1_unpaired="results/trimmomatic/{sample}_1_unpaired_trimmed.fastq",
        r2_unpaired="results/trimmomatic/{sample}_2_unpaired_trimmed.fastq"
    log:
        "logs/trimmomatic/{sample}.log"
    params:
        # Specify the trimmer options
        trimmer=[
            "ILLUMINACLIP:../envs/trimmomatic-0.39/adapters/TruSeq3-PE.fa:2:30:10",
            "SLIDINGWINDOW:5:20",
            "LEADING:5",
            "TRAILING:5",
            "MINLEN:50"],
        compression_level="-9"
    threads: 16
    resources:
        mem_mb=1024
    wrapper:
        "v3.5.2/bio/trimmomatic/pe"

rule mergeFastQ:
    input:
        r1="results/trimmomatic/{sample}_1_paired_trimmed.fastq",
        r2="results/trimmomatic/{sample}_2_paired_trimmed.fastq"
    output:
        "results/sortmerna/merged/{sample}_interleaved.fastq"
    conda:
        "../envs/sortmerna.yaml"
    shell:
        "workflow/scripts/merge-paired-reads.sh {input.r1} {input.r2} {output}"

rule sortMeRNA:
    """
    SortMeRNA uses a Work directory with the default location $USER/sortmerna/run/. Sortmerna also checks kvdb directory. 
    This directory has to be Empty prior each new run. Therefore, we have to empty this before starting a new run.
    """
    input:
        ref1=config['rRNA_databases'] + "silva-bac-16s-id90.fasta",
        ref2=config['rRNA_databases'] + "silva-bac-23s-id98.fasta",
        ref3=config['rRNA_databases'] + "silva-arc-16s-id95.fasta", 
        ref4=config['rRNA_databases'] + "silva-arc-23s-id98.fasta",
        ref5=config['rRNA_databases'] + "silva-euk-18s-id95.fasta",
        ref6=config['rRNA_databases'] + "silva-euk-28s-id98.fasta",
        ref7=config['rRNA_databases'] + "rfam-5s-database-id98.fasta", 
        ref8=config['rRNA_databases'] + "rfam-5.8s-database-id98.fasta",
        sample="results/sortmerna/merged/{sample}_interleaved.fastq",
    output:
        nonRNA="results/sortmerna/{sample}_non_rRNA.fastq",
        alignedRNA="results/sortmerna/{sample}_rRNA.fastq",
    conda:
        "../envs/sortmerna.yaml",
    threads: 
        threads=config['threads'],
    shell:
        """
        rm -rf ~/sortmerna/run/kvdb/*
        sortmerna --reads {input.sample} --ref {input.ref1} --ref {input.ref2} --ref {input.ref3} --num_alignments 1 --fastx --aligned {output.alignedRNA} --other {output.nonRNA} -a {threads} -m 64000 --paired_in -v
        mv {output.alignedRNA}.fq {output.alignedRNA} &&
        mv {output.nonRNA}.fq {output.nonRNA}
        """

rule unmergeFastQ:
    input:
        "results/sortmerna/{sample}_non_rRNA.fastq",
    output:
        r1="results/sortmerna/unmerged/{sample}_non_rRNA_1.fastq",
        r2="results/sortmerna/unmerged/{sample}_non_rRNA_2.fastq",
    shell:
        "workflow/scripts/unmerge-paired-reads.sh {input} {output.r1} {output.r2}"

rule trinity_assembly:
    """
    Trinity is a de novo transcriptome assembler
    """
    input:
        left=["results/sortmerna/unmerged/{sample}_non_rRNA_1.fastq"],
        right=["results/sortmerna/unmerged/{sample}_non_rRNA_2.fastq"],
    output:
        dir=temp("/tmp/{sample}_trinity_out"),
        fas="results/trinity/{sample}_trinity.fasta",
        map="results/trinity/{sample}_trinity.fasta.gene_trans_map"
    params:
        extra="",
    threads:
        config['threads']
    resources:
        mem_gb=3
    wrapper:
        "v3.5.3/bio/trinity"